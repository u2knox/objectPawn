
#if defined _inc_tdw_cjump
	#undef _inc_tdw_cjump
#endif

#if defined _TDW_C_JUMP
	#endinput
#endif

#define _TDW_C_JUMP

enum JmpBuf
{
	jhea,      // heap ptr.
	jfrm,      // frame ptr.
	jstk,      // stack ptr.
	jjump,     // the instruction pointer to the return address
	jretval,   // reference to the variable
};

stock setjmp(env[JmpBuf], &retval)
{
	static
		hea,
		frm,
		stk,
		rvip,
		rval;

	#emit lctrl 2
	#emit stor.pri hea

	#emit load.s.pri 0
	#emit stor.pri frm

	#emit lctrl 4
	#emit stor.pri stk

	#emit load.s.pri 4
	#emit stor.pri rvip

	#emit load.s.pri retval
	#emit stor.pri rval

	env[jfrm]    = frm;
	env[jhea]    = hea;
	env[jstk]    = stk;
	env[jjump]   = rvip;
	env[jretval] = rval;

	return 0;
}

stock longjmp(const env[JmpBuf], val)
{
	static
		hea,
		frm,
		stk,
		rvip,
		rval,
		args;

	hea = env[jhea];
	frm = env[jfrm];
	stk = env[jstk];
	rvip = env[jjump];
	rval = env[jretval];

	#emit load.s.pri val
	#emit sref.pri rval

	#emit load.s.pri 0
	#emit add.c 8
	#emit load.i
	#emit stor.pri args

	if (args > 4) {
		stk += args + 8;
	} {}

	#emit load.pri frm
	#emit sctrl 5

	#emit load.pri hea
	#emit sctrl 2

	#emit load.pri stk
	#emit sctrl 4

	#emit load.pri rvip
	#emit sctrl 6

	return 0;  // make compiler happy
}
